buildscript {
    repositories {
        jcenter()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}

apply plugin: 'crashlytics'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        def stdout2 = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout2
        }
        if (stdout2.toString().trim() == 'master') {
            return stdout.toString().trim()
        } else {
            return stdout.toString().trim() + '(' + stdout2.toString().trim() + ')'
        }
    }
    catch (ignored) {
	return 'invalid';
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "edu.uci.crayfis"
        minSdkVersion 19
        targetSdkVersion 25
        renderscriptTargetApi 19
        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        release {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.google.protobuf:protobuf-java:3.4.0'
    compile 'com.jjoe64:graphview:3.1.3'
    compile 'com.crashlytics.android:crashlytics:1.1.13'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.google.android.gms:play-services-location:11.0.4'
    compile project(':libraries:opencv')
}

